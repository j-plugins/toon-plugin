{
  parserClass = "com.github.xepozz.toon.language.parser.ToonParser"

  extends = "com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix = "Toon"
  psiImplClassSuffix="Impl"
  psiPackage="com.github.xepozz.toon.language.psi"
  psiImplPackage="com.github.xepozz.toon.language.psi.impl"

  elementTypeHolderClass="com.github.xepozz.toon.language.psi.ToonTypes"
  elementTypeClass = "com.github.xepozz.toon.language.psi.ToonElementType"
  tokenTypeClass = "com.github.xepozz.toon.language.psi.ToonTokenType"

  psiImplUtilClass="com.github.xepozz.toon.language.psi.impl.ToonImplUtil"
  tokens=[
    LPAREN="LPAREN"
    RPAREN="RPAREN"
  ]
}

file ::= entry*

private entry ::= table | keyValueMap | COMMENT | EOL

table ::= tableDefinition DELIMITER (iterable | EOL array)
{
    pin=2
}

tableDefinition ::= key LBRACKET '#'? NUMBER RBRACKET (LBRACE iterable RBRACE)?
{
    pin=2
}

array ::= keyValueArray+ | keyValueMap+ | valuesList

valuesList ::= iterable (EOL iterable)*

keyValueArray ::= keyValueArrayItem (EOL keyValueArrayItem)*

keyValueArrayItem ::= DASH (keyValueMap | iterable)
{
    pin=1
}

keyValueMap ::= key DELIMITER (iterable | EOL array)
{
    pin=2
}

key ::= TEXT

iterable ::= value (COMMA value)*

value ::= TEXT | NULL | FALSE | TRUE | NUMBER
{
    implements=["com.intellij.psi.PsiLiteralValue"]
    methods=[getValue getReferences]
}
