{
  parserClass = "com.github.xepozz.toon.language.parser.ToonParser"

  extends = "com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix = "Toon"
  psiImplClassSuffix = "Impl"
  psiPackage = "com.github.xepozz.toon.language.psi"
  psiImplPackage = "com.github.xepozz.toon.language.psi.impl"

  elementTypeHolderClass = "com.github.xepozz.toon.language.psi.ToonTypes"
  elementTypeClass = "com.github.xepozz.toon.language.psi.ToonElementType"
  tokenTypeClass = "com.github.xepozz.toon.language.psi.ToonTokenType"

  psiImplUtilClass = "com.github.xepozz.toon.language.psi.impl.ToonImplUtil"

  tokens = [
    LPAREN = "LPAREN"
    RPAREN = "RPAREN"
    LBRACE = "LBRACE"
    RBRACE = "RBRACE"
    LBRACKET = "LBRACKET"
    RBRACKET = "RBRACKET"
    DELIMITER = "DELIMITER"
    COMMA = "COMMA"
    DASH = "DASH"
    SHARP = "SHARP"
    EOL = "EOL"
    COMMENT = "COMMENT"
    TEXT = "TEXT"
    NUMBER = "NUMBER"
    NULL = "NULL"
    BOOLEAN = "BOOLEAN"
    INDENT = "INDENT"
    DEDENT = "DEDENT"
    BLOCK = "BLOCK"
  ]
}

file ::= entry*

private entry ::=
    object_field
  | table
  | list_item
  | COMMENT
  | EOL

object_field ::= key DELIMITER field_value {
  pin = 2
}
private field_value ::=
    value_list
  | nested_object
  | array

value_list ::= value (COMMA value)*

nested_object ::= EOL INDENT entry+ DEDENT {
    pin = 2
}

array ::= list_item (EOL list_item)+ {
    pin = 1
}

list_item ::= DASH list_item_content {
  pin = 1
}

private list_item_content ::=
    value_list
  | key DELIMITER field_value

table ::= table_header DELIMITER table_data {
  pin = 2
}

table_header ::= key LBRACKET table_size RBRACKET field_list? {
    pin=4
}

table_size ::= SHARP? NUMBER

field_list ::= LBRACE field_names RBRACE {
  pin = 1
}

field_names ::= key (COMMA key)*

table_data ::= table_data_inline | table_data_multiline

table_data_inline ::= table_data_value
table_data_multiline ::= EOL INDENT (table_data_multiline_item EOL)* DEDENT {
    pin = 2
}

private table_data_multiline_item ::= table_data_value | COMMENT
private table_data_value ::= value_list

table_row ::= value (COMMA value)*

key ::= TEXT

value ::=
    TEXT
  | NUMBER
  | NULL
  | BOOLEAN {
    implements = ["com.intellij.psi.PsiLiteralValue"]
    methods = [getValue getReferences]
  }